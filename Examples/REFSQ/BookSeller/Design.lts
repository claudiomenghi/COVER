// ---------------------------------------------------
// CONTAINS THE INITIAL BOOK SELLER DESIGN
// ---------------------------------------------------

set Actions = { cRequestQuote,          // it is generated when the user has requested a quote 
                sProvideQuote,          // it is generated when WH has provided a quote
                cGetBookAvail,          // the client asks if a book is sold by the WH 
                cLeft,                  // the client has left the WH store
                cOrder,                 // the client has performed the order
                sRetBookAvail,          // the WH returns the book availability
                sProvidePrice,          // the WH provides the price for the item
                sPlaceOrder,            // the WH has placed the order to its supplier
                cBookOrdered,           // the client has ordered the book and terminated the payment procedure
                sRetBookOrd,             // the book is ready to be delivered
				sSubmitReceipt,         // the system has sent the receipt (electronically or physically)
                sCourierStart,          // the courier started
                sWaitBook,              // the WH is waiting for the book
                sBookDelivered,         // the book is delivered to the customer
                cMoneyOrder,            // the client wants to pay through bank transfer money
                cSendMoney,             // the client has sent the bank transfer
                sReceivedMoney,         // the WH received the bank transfer
                sHasCCAAuth,            // the client has requested a credit card transfer and has been correctly authenticated
                sChargeCreditCard,      // the system has charded the user credit card
                sReceiptPrinted,        // the system printed the receipt
                sReceiptDelivered,      // the receipt has been deliveder to the client (physically or electronically)
                sSendElecReceipt,        // the electronic receipt has been sent to the user
                sSendPhysicalReceipt,   // the physical receipt has been sent to the user
                bookReceived,           // the WH received the book from the seller
                orderClosed,            // the order has been closed
                end,                    // the order ended with no success
                sBookNotAvailable       // the book is not available in the WH
			}

// initial book seller desing
D1=		QUOTEMANAGER,
        QUOTEMANAGER=(  		cRequestQuote?      ->  QUOTEMANAGER        		|
                        		sProvideQuote?      ->  QUOTEMANAGER        		|
                       			cGetBookAvail?      ->  AVAILABILITYMANAGER         |
                        		cLeft   			->  ENDSTATE 					|
                        		cOrder?     		->  PAYMENTMANAGER      		),

		AVAILABILITYMANAGER=(	sRetBookOrd? 		-> QUOTEMANAGER 				|
								sProvidePrice?		-> AVAILABILITYMANAGER 			|
								sPlaceOrder?	  	-> AVAILABILITYMANAGER 			|
								sBookNotAvailable?	-> AVAILABILITYMANAGER			|
								bookReceived?  	    -> AVAILABILITYMANAGER 			|
								sRetBookAvail?	        -> DELIVERYMANAGER     			),

        DELIVERYMANAGER=(   	sSubmitReceipt?		    -> 	DELIVERYMANAGER				|
								sCourierStart?		    ->	DELIVERYMANAGER				|
								sReceiptDelivered?	    ->	DELIVERYMANAGER				|
								sSendElecReceipt?	    ->	DELIVERYMANAGER				|
								sSendPhysicalReceipt?	->	DELIVERYMANAGER				|
								sWaitBook?	            ->  AVAILABILITYMANAGER 		|
                            	sBookDelivered	        ->  ORDERCLOSED					),

        PAYMENTMANAGER=(    	cMoneyOrder?		->	PAYMENTMANAGER				|
								cSendMoney?			->	PAYMENTMANAGER				|
								sReceivedMoney?		->	PAYMENTMANAGER				|
								sChargeCreditCard?	->	PAYMENTMANAGER				|
								sHasCCAAuth?		->	PAYMENTMANAGER				|
								sReceiptPrinted?	->	PAYMENTMANAGER				|
								cLeft 				->	ENDSTATE					|
								cBookOrdered?		->	DELIVERYMANAGER				),

        ORDERCLOSED=(   		orderClosed  		->  ORDERCLOSED					),

        ENDSTATE=(   			end     			-> 	ENDSTATE					).


// refinement of the book seller desing
D2=		QUOTEMANAGER,
        QUOTEMANAGER=(  		cRequestQuote?        ->  QUOTEMANAGER        		|
                        		sProvideQuote?        ->  QUOTEMANAGER        		|
                       			cGetBookAvail         -> 	Q1 						|
                        		cLeft   			  ->  ENDSTATE 					|
                        		cOrder?     		  ->  PAYMENTMANAGER      		),

		Q1=(					sBookNotAvailable		->	Q2						|
								sRetBookOrd		->	QUOTEMANAGER				),
		
		Q2=(					sPlaceOrder			->	Q3							),

		Q3=(					sRetBookOrd		->	QUOTEMANAGER				),

		Q4=(					bookReceived		->	Q5							),

		Q5=(					sProvidePrice		->	Q6							),

		Q6=(					sRetBookAvail?			->  DELIVERYMANAGER				),

        DELIVERYMANAGER=(   	sSubmitReceipt?		    -> 	DELIVERYMANAGER				|
								sCourierStart?		    ->	DELIVERYMANAGER				|
								sReceiptDelivered?	    ->	DELIVERYMANAGER				|
								sSendElecReceipt?	    ->	DELIVERYMANAGER				|
								sSendPhysicalReceipt?	->	DELIVERYMANAGER				|
								sWaitBook	        ->  Q4 							|
                            	sBookDelivered?	    ->  ORDERCLOSED					),

        PAYMENTMANAGER=(    	cMoneyOrder?		->	PAYMENTMANAGER				|
								cSendMoney?			->	PAYMENTMANAGER				|
								sReceivedMoney?		->	PAYMENTMANAGER				|
								sChargeCreditCard?	->	PAYMENTMANAGER				|
								sHasCCAAuth?		->	PAYMENTMANAGER				|
								sReceiptPrinted?	->	PAYMENTMANAGER				|
								cLeft 				->	ENDSTATE					|
								cBookOrdered		->	DELIVERYMANAGER				),

        ORDERCLOSED=(   		orderClosed  		->  ORDERCLOSED					),

        ENDSTATE=(   			end     			-> 	ENDSTATE					).


// refinement of the book seller desing
D3=		QUOTEMANAGER,
        QUOTEMANAGER=(  		cRequestQuote?      ->  QUOTEMANAGER        		|
                        		sProvideQuote?      ->  QUOTEMANAGER        		|
                       			cGetBookAvail      -> 	Q1 						    |
                        		cLeft   			->  ENDSTATE 					|
                        		cOrder?     			->  PAYMENTMANAGER      		),

		Q1=(					sBookNotAvailable		->	Q2						|
								sRetBookOrd		->	QUOTEMANAGER				),
		
		Q2=(					sPlaceOrder			->	Q3							),

		Q3=(					sRetBookOrd		->	QUOTEMANAGER				),

		Q4=(					bookReceived		->	Q5							),

		Q5=(					sProvidePrice		->	Q6							),

		Q6=(					sRetBookAvail			->  Q8							),

		Q7=(					sWaitBook	        ->  Q4 							),

		Q8=(					sSendPhysicalReceipt	->	Q9						|
								sSendElecReceipt		->	Q9							),

		Q9=(					sSubmitReceipt		->	Q10							),

		Q10=(					sCourierStart		->	Q11							),

		Q11=(					sReceiptDelivered	->	Q12      					),
        
        Q12=(					sBookDelivered		->	ORDERCLOSED					),

        PAYMENTMANAGER=(    	cMoneyOrder?		->	PAYMENTMANAGER				|
								cSendMoney?			->	PAYMENTMANAGER				|
                                sChargeCreditCard?	->	PAYMENTMANAGER				|
								sHasCCAAuth?		->	PAYMENTMANAGER				|
								sReceivedMoney?		->	PAYMENTMANAGER				|
								sReceiptPrinted?	->	PAYMENTMANAGER				|
								cLeft 				->	ENDSTATE					|
								cBookOrdered		->	Q7				),

        ORDERCLOSED=(   		orderClosed  		->  ORDERCLOSED					),

        ENDSTATE=(   			end     			-> 	ENDSTATE					).

|| D1_PROC=(D1).

|| D2_PROC=(D2).

|| D3_PROC=(D3).


// quote management fluents
fluent F_cRequestQuote=<cRequestQuote, Actions\{cRequestQuote}>


// book availability management fluents
fluent F_sBookNotAvailable=<sBookNotAvailable, Actions\{sBookNotAvailable}>
fluent F_sRetBookAvail=<sRetBookAvail, Actions\{sRetBookAvail}> 
fluent F_bookReceived=<bookReceived, Actions\{bookReceived}> 



// book delivering fluents
fluent F_sBookDelivered=<sBookDelivered, Actions\{sBookDelivered}> 
fluent F_sPlaceOrder=<sPlaceOrder, Actions\{sPlaceOrder}>
fluent F_sCourierStart=<sCourierStart, Actions\{sCourierStart}> 


// receipt management fluents
fluent F_sSubmitReceipt=<sSubmitReceipt, Actions\{sSubmitReceipt}>
fluent F_sReceiptDelivered=<sReceiptDelivered, Actions\{sReceiptDelivered}>
fluent F_sSendPhysicalReceipt=<sSendPhysicalReceipt, Actions\{sSendPhysicalReceipt}>


// money management fluents
fluent F_sReceivedMoney=<sReceivedMoney, Actions\{sReceivedMoney}>
fluent F_sChargeCreditCard=<sChargeCreditCard, Actions\{sChargeCreditCard}>
fluent F_cSendMoney=<cSendMoney, Actions\{cSendMoney}>
fluent F_sProvidePrice=<sProvidePrice, Actions\{sProvidePrice}>
fluent F_sProvideQuote=<sProvideQuote, Actions\{sProvideQuote}>

fluent F_orderClosed=<orderClosed, Actions\{orderClosed}>
fluent F_sWaitBook=<sWaitBook, Actions\{sWaitBook}>

// G2: if requested the system provides the quote OK
assert G2=([](F_cRequestQuote-><>F_sProvideQuote))

// G3: if the WH orders a book, it is finally delivered to the WH OK
assert G3=((!F_orderClosed) W ((F_bookReceived) && (!F_orderClosed)))

// G4: if a book is not available at the WH, it is ordered OK
assert G4=([](F_sBookNotAvailable->(<>F_sPlaceOrder)))

// G5: after a book is ordered it is delivered at the WH OK
assert G5=([](F_sWaitBook->(<>F_bookReceived)))

// G6: The book is finally delivered to the customer. OK
assert G6=((!F_orderClosed) W ((F_sBookDelivered) && (!F_orderClosed)))

// G7: if request the receipt is placed in the shipment 
assert G7=([](F_sSubmitReceipt->(<>F_sReceiptDelivered)))

// G8: the payment of the customer is finally received
assert G8=((!F_orderClosed) W ((F_sReceivedMoney) && (!F_orderClosed)))

// G9: the money are charged over the CC
assert G9=((!F_orderClosed) W ((F_sChargeCreditCard) && (!F_orderClosed)))

// G10: An order to be closed requires the payment of the user 
assert G10=((!F_orderClosed) W ((F_cSendMoney) && (!F_orderClosed)))

// G11: a physical or electronic receipt is sent
assert G11=((!F_orderClosed) W ((F_sSubmitReceipt) && (!F_orderClosed)))

// G12: the physical receipt is finally received
assert G12=([]((F_sSendPhysicalReceipt && !F_orderClosed)->(!F_orderClosed U (F_sReceiptDelivered && !F_orderClosed))))

// G13: the receipt is received
assert G13=((!F_orderClosed) W ((F_sReceiptDelivered) && (! F_orderClosed)))

// G18: the supplier provides the price before the quotes
assert G18=((!F_sProvidePrice) W ((F_sProvideQuote) && (!F_sProvidePrice)))

// G24: charge CC can be done only before delivering to courier
assert G24=([](F_sBookDelivered->([](!F_sChargeCreditCard))))

// G25: deliver to courier after get CC authorization
assert G25=([](F_sCourierStart->[](!F_sChargeCreditCard)))

// G26: The receipt is sent after the payment
assert G26=([]( (! F_sReceivedMoney) || (<>(F_sReceivedMoney && <>F_sReceiptDelivered))))

